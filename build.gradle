plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.40' apply false
    id 'org.jetbrains.dokka' version '0.9.16'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven-publish'
    id 'signing'
}

group = 'io.opencubes'
version = '1.1.0'

allprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'kotlin'

    dependencies {
        compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.+'
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenKotlin(MavenPublication) {
            artifactId = archivesBaseName
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = project.name
                description = 'A basic event system'
                url = 'https://github.com/ocpu/brev-kotlin#readme'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'ocpu'
                        name = 'Martin HÃ¶vre'
                        email = 'martin.hovre@opencubes.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ocpu/brev-kotlin.git'
                    developerConnection = 'scm:git:ssh://github.com:ocpu/brev-kotlin.git'
                    url = 'http://github.com/ocpu/brev-kotlin/tree/master'
                }
            }
        }
    }
    repositories {
        maven {
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenKotlin
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenKotlin']
    pkg {
        repo = 'maven'
        name = project.name
        websiteUrl = 'https://github.com/ocpu/brev-kotlin'
        issueTrackerUrl = 'https://github.com/ocpu/brev-kotlin/issues'
        vcsUrl = 'https://github.com/ocpu/brev-kotlin.git'
        licenses = ['MIT']
//        publish = true
        githubRepo = 'ocpu/brev-kotlin'
        version {
            name = project.version
            vcsTag = project.version
            gpg {
                sign = true
                passphrase = property("signing.password")
            }
        }
    }
}
